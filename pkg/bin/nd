#!/usr/bin/env zsh
set -eu -o pipefail

# a flake reference
# only local flakes are allowed, eg
# - ../somewhere/here
# - ../somewhere/here#name
# - path:../somewhere/here
local _flake

# the plain folder of the flake, without any 'path:' or '#name' elements
local _folder

function ensure-flake {
    if [[ $_flake == '' ]]; then
        _flake=$(setopt extended_glob null_glob; echo (../)#flake.nix(:a:h))
        _folder=$_flake
        if [[ $_flake == '' ]]; then
            echo "No flake found at $PWD or up." >&2
            exit 1
        fi
    fi
}

function nd-build nd-b {
    ensure-flake

    if [[ $_flake == '-' ]]; then
        return
    fi

    if which flupq &>/dev/null && [[ -e $_folder/flake.lock ]]; then
        mkdir -p $_folder/.nd
        flupq --offline --quiet --quiet --output-lock-file $_folder/.nd/flup.lock --flake $_flake
        if [[ -e _folder/.nd/flup.lock ]]; then
            if ! diff $_folder/flake.lock $_folder/.nd/flup.lock &>/dev/null; then
                echo
                print -P -- '%F{1}%Snd: `flup` would change "flake.lock".%s%f'
            fi
            rm $_folder/.nd/flup.lock
        else
            print -P -- '%F{1}%Snd: `flup` didnt produce a lock file.%s%f'
        fi
    fi

    mkdir -p $_folder/.nd
    # double --quiet doesnt warn about dirty git, but might hide other things too?
    nix develop $_flake --quiet --quiet --profile $_folder/.nd/dev --command true
    cp $_folder/flake.lock $_folder/.nd/flake.lock
}

function nd-run nd-r {
    ensure-flake

    if [[ $_flake == '-' ]]; then
        exec ${@:-zsh}
    fi

    if [[ ! -e $_folder/.nd/dev ]]; then
        nd-build
    fi

    if [[ -v nd ]]; then
        nd=$_folder:$nd
    else
        export nd=$_folder
    fi

    if [[ $(( $(date '+%s') - $(stat --format='%Y' $_folder/.nd/dev) > 7 * 24 * 60 * 60 )) == 1 ]]; then
        echo
        print -P -- '%F{1}%Snd: No rebuild since more than a week.%s%f'
    fi

    if [[ -e $_folder/flake.lock && -e $_folder/.nd/flake.lock ]] && ! diff $_folder/flake.lock $_folder/.nd/flake.lock &>/dev/null; then
        echo
        print -P -- '%F{1}%Snd: Profile is from a different "flake.lock".%s%f'
    fi

    exec nix develop $_folder/.nd/dev --command ${@:-zsh}
}

function nd-shell nd-s {
    nd-run zsh $@
}

function nd-env nd-e {
    nd-at ${nd_env:--} $@
}

function nd-tmux {
    [[ ! -v TMUX ]]
    h=${1:-$PWD}
    h=${h:a}
    cd $h
    ensure-flake
    if [[ $_flake == '-' ]]; then
        exec tmux new-session -s ${h:a:t} $@
    else
        exec tmux new-session -s ${h:a:t} -e h=$h -e nd_env=$_flake $@[2,-1]
    fi
}

function nd-at {
    _flake=$1
    _folder=
    if [[ $_flake != '-' ]]; then
        if [[ ! $_flake =~ '(path:)?(.*)(#.*)?' ]]; then
            echo "Cannot parse flake reference $_flake" >&2
            exit 1
        fi
        _folder=$match[2]
    fi
    nd-${2:-shell} $@[3,-1]
}

nd-${1:-shell} $@[2,-1]
